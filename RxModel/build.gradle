apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply from: file('../library.gradle')

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

dependencies {
    // Language
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // Test
    testCompile "junit:junit:4.12"
    // Rx
    compile "io.reactivex.rxjava2:rxjava:2.1.0"
    // Lock
    compile "com.googlecode.concurrent-locks:concurrent-locks:1.0.0" // https://github.com/npgall/concurrent-locks
}

def library = project.ext.library
group = library.groupId
version = library.version

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    //noinspection GroovyAccessibility
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}